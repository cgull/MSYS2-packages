# Maintainer: James Ross-Gowan <rossymiles@gmail.com>

pkgname=mosh
pkgver=1.2.6
pkgrel=1
pkgdesc="Mobile shell, surviving disconnects with local echo and line editing"
arch=('i686' 'x86_64')
url='https://mosh.mit.edu/'
groups=('net-utils')
license=('GPL3')
depends=('protobuf' 'ncurses' 'zlib' 'libopenssl' 'openssh' 'perl')
makedepends=('protobuf-devel' 'ncurses-devel' 'zlib-devel' 'openssl-devel' 'bash-completion')
source=("https://${pkgname}.mit.edu/${pkgname}-${pkgver}.tar.gz")
sha256sums=('7e82b7fbfcc698c70f5843bb960dadb8e7bd7ac1d4d2151c9d979372ea850e85')
options=(!emptydirs)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --host=${CHOST} \
    --prefix=/usr \
    --enable-completion
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
